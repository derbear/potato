(load "bind-native.pot")

(define macro
  (func (x) (cast x 'MACRO)))

(define error
  (func (x) (cast x 'ERROR)))

;;; List utils

(define list (func a a))

;; return -1 * number of cells if not nil-terminated e.g. (1 2 3 . 4) returns -4
;; return nil if malformed

(define length
  (func (list)
    (define length-helper
      (func (remaining count)
	(if remaining
	    (if (= (type remaining) 'CELL)
		(length-helper (rest remaining) (+ 1 count))
		(* -1 (+ 1 count)))
	    count)))
    (length-helper list 0)))

(define last
  (func (list)
    (if list
	(if (rest list)
	    (last (rest list))
	    (first list))
	list))) ; nil

(define second
  (func (list)
    (if list
	(if (rest list)
	    (first (rest list))
	    (error "cannot get second of length-1 list"))
	(error "cannot get second of length-0 list"))))

(define append
  (func (l1 l2)
    (if l1
	(cons (first l1) (append (rest l1) l2))
	l2)))

(define map
  (func (f list)
    (if list
	(cons (f (first list)) (map f (rest list)))
	list))) ; nil

;;; Logic and comparators

(define not
  (func (pred)
    (if pred
	nil
	'true)))

(define andf
  (func (p1 p2)
    (list 'if p1
	  (list 'if p2 ''t nil)
	  nil)))
(define and (macro andf))

(define orf
  (func (p1 p2)
    (list 'if p1
	  ''t
	  (list 'if p2 ''t nil))))
(define or (macro orf))

(define >
  (func (a b)
    (< b a)))

(define <=
  (func (a b)
    (or (< a b) (= a b))))

(define >=
  (func (a b)
    (or (> a b) (= a b))))

;; (cond (<cond1> <expr1>)
;;       (<cond2> <expr2>)
;;       ...
;;       (<condn> <exprn>)
;;       (else <expr'>))
;; if else clause is missing, then implicit (else nil)
;; generates equivalent chained if-expression
(define condf
  (func switch-list
    (define make-cond-statement
      (func (conditional consequence alternative)
	(list 'if conditional consequence alternative)))
    (define condf-helper
      (func (switch-list)
	(if (rest switch-list)
	    (make-cond-statement (first (first switch-list))
				 (second (first switch-list))
				 (condf-helper (rest switch-list)))
	    (if (= (first (first switch-list)) 'else) ; (cond (else <expr>))
		(second (first switch-list))
		nil))))
    (condf-helper switch-list)))

(define cond (macro condf))

;;; Control flow

;; evaluate expressions under once-evaluated local name-bindings
;; args should look like (<bindings> <expr1> <expr2> ...)
;; bindings looks something like ((a . 3) (b . 5))
;; cannot do dependent bindings like ((a . 3) (b . (+ a 2)))
;;;;;
;;    Generates:
;;    ((func param-list expressions) value-list)))
;;;;;
(define letf
  (func args
    (define bindings (first args))
    (define param-list (map first bindings))
    (define value-list (map (func (pair) (rest pair)) bindings))
    (define expressions (rest args))
    (cons (cons 'func (cons param-list expressions)) value-list)))

(define let (macro letf))

;;; I/O

(define display (func (str) (c-fputs c-stdout str) nil))
(define display-error
  (func (str) (display "ERROR: ") (display str) (display "\n")))

;;; Main code

(define global-frame (frame))

(define make-repl
  (func ()
    (define repl-frame (frame))
    (define repl
      (func (prompt)
	(display prompt)
	(protect (print (eval (read) repl-frame)) display-error)
	(repl prompt)))
    repl))

(if (= 1 (length program-arguments))
    ((make-repl) "> ")
    (load (first (rest program-arguments))))
